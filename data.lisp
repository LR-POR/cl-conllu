
(in-package :cl-conllu)

(defclass token ()
  ((id      :initarg :id
	    :accessor token-id)
   (form    :initarg :form
	    :accessor token-form)
   (lemma   :initarg :lemma
	    :accessor token-lemma)
   (upostag :initarg :upostag
	    :initform "_"
	    :accessor token-upostag)
   (xpostag :initarg :xpostag
	    :initform "_"
	    :accessor token-xpostag)
   (feats   :initarg :feats
	    :initform "_"
	    :accessor token-feats)
   (head    :initarg :head
	    :initform "_"
	    :accessor token-head)
   (deprel  :initarg :deprel
	    :initform "_"
	    :accessor token-deprel)
   (deps    :initarg :deps
	    :initform "_"
	    :accessor token-deps)
   (misc    :initarg :misc
	    :initform "_"
	    :accessor token-misc)))

(defclass mtoken ()
  ((start   :initarg :start
	    :accessor mtoken-start)
   (end     :initarg :end
	    :accessor mtoken-end)
   (form    :initarg :form
	    :accessor mtoken-form)
   (misc    :initarg :misc
	    :initform "_"
	    :accessor mtoken-misc)))

(defclass sentence ()
  ((start   :initarg :start
	    :initform 0
	    :accessor sentence-start)
   (meta    :initarg :meta
	    :initform nil
	    :accessor sentence-meta)
   (tokens  :initarg :tokens
	    :initform nil
	    :accessor sentence-tokens)
   (mtokens :initarg :mtokens
	    :initform nil
	    :accessor sentence-mtokens)))


(defun sentence-meta-value (sentence meta-field)
  (cdr (assoc meta-field (sentence-meta sentence) :test #'equal)))


(defun sentence->text (sentence)
  (labels ((forma (obj lst)
	     (if (search "SpaceAfter=No" (slot-value obj 'misc))
		 (cons (slot-value obj 'form) lst)
		 (cons " " (cons (slot-value obj 'form) lst))))
	   (aux (tokens mtokens ignore response)
	     (cond 
	       ((and (null tokens) (null mtokens))
		(if (equal " " (car response))
		    (reverse (cdr response))
		    (reverse response)))

	       ((and ignore (< (token-id (car tokens)) ignore))
		(aux (cdr tokens) mtokens ignore response))
	       ((and ignore (equal (token-id (car tokens)) ignore))
		(aux (cdr tokens) mtokens nil response))
      
	       ((and mtokens (<= (mtoken-start (car mtokens)) (token-id (car tokens))))
		(aux tokens (cdr mtokens)
				   (mtoken-end (car mtokens))
				   (forma (car mtokens) response)))
	       (t
		(aux (cdr tokens) mtokens ignore (forma (car tokens) response))))))
    (format nil "狺ㄡ貘箦铘孱沐麸脲铙箦铘孱沐箦铘孱沐眙镫孱箦铘孱沐铋铋飑┅ㄤ彐躅箦铘孱沐鲠扉淇箦铘孱沐ㄡ钿ㄥ鲥蝙灬礅溽綦铒ㄥ聃犰箪雉鲠祯綦ч洎箪雉鲠祯綦ц遽洎┅箦铘孱沐麸脲铙箦铘孱沐┅箫礤灬礅溽綦ㄡ钿ㄥ聃犰箪雉鲠祯綦ц遽洎ㄥ聃犰Ⅱ镲簪箪雉鲠祯綦т屦蝈飑┅箦铘孱沐麸脲铙箦铘孱沐┅箦铘孱沐礤翎鲠祯箦铘孱沐Ⅳ屮簪ㄥ聃犰箦铘孱沐礤翎鲠祯箦铘孱沐Ⅳ屮簪箦铘孱沐爵屮箦铘孱沐┅箦铘孱沐礤翎鲠祯箦铘孱沐Ⅲ孱暨殇┅ㄤ彐躅箦铘孱沐箝箦铘孱沐戾铉翳箦铘孱沐麸脲铙箦铘孱沐┅ㄤ彐躅箦铘孱沐句邋箦铘孱沐脲骖脲灬忮祗è孱篚蝈扉篝脲ㄩ簌礅镬脲扉篝骖脲脲┅ㄩㄦ躅泗轱铕骖脲ㄤ邋瓠狨箦铘孱沐蝻雉箦铘孱沐箦铘孱沐骖脲ㄩ矧簌礅镬骖脲扉篝骖脲┅ㄤ邋瓠狨箦铘孱沐蝻雉箦铘孱沐箦铘孱沐灬礅溽綦戾è秕祜镳骘轭ㄥ铙躜瀛扉篝骖脲泔祆邈箪雉鲠祯綦氅┅ㄩㄡ钿扉篝秕舂戾铉翳秕舂┅ㄣ狎秕舂秕舂┅┅┅ㄤ彐躅溴屦狨蝻雉箦铘孱沐骖脲扉篝ㄦ躅汜祆骖脲蝻雉祜镳骘汨殪轭麸脲瞽汨殪蝻雉箦铘孱沐泔祆邈扉篝箪雉鲠祯汨殪т屦蝈飑ㄩ麸脲瞽汨殪汨殪箦铘孱沐ㄤ邋瓠狨汨殪箦铘孱沐骖脲ㄦ躅汜祆骖脲汨殪洎┅┅ㄤ彐躅箦铘孱沐蝻雉箦铘孱沐ㄣ狎蝈盹鲥殒铒灬礅溽綦ㄥ聃犰阿箪雉鲠祯綦ц遽洎┅箦铘孱沐麸脲铙箦铘孱沐┅┅ㄤ彐躅麸脲瞽汨殪麸脲箦铘孱沐蝈盹鲥殒铒灬礅溽綦ㄥ聃犰箪雉鲠祯綦ц遽洎箪雉鲠祯麸脲ч洎┅箦铘孱沐麸脲铙箦铘孱沐┅ㄤ彐躅轭箦螋眙镫孱箦铘孱沐眙镫孱脲ㄩ姝屮轶趔т锃铒翳轭绌磲疸灬礅溽眙镫孱戾è屮轶糸铉眙镫孱ㄦ轭洵殒灬礅溽ㄡ钿ㄥ眙镫孱篝狎眙镫孱篝狎眙镫孱┅ㄥ眙镫孱孱眙镫孱孱眙镫孱┅┅箦铘孱沐眙镫孱箦铘孱沐┅┅ㄩ屮轶糸铉眙镫孱ㄣ狍殒屮轶趔èт锃铒翳轭绌铋飑èэ鲥蝼蜷翦蝈盹鲥屮轶糸铉眙镫孱箦铘孱沐眙镫孱箦铘孱沐┅瘐箬眙镫孱箦铘孱沐眙镫孱箦铘孱沐┅èп滗瘐箬眙镫孱箦铘孱沐眙镫孱箦铘孱沐┅┅ㄡ钿瘐箬眙镫孱箦铘孱沐眙镫孱箦铘孱沐┅┅┅眙镫孱螬箦翩箦铘孱沐眙镫孱箦铘孱沐箫螋箦铘孱沐眙镫孱箦铘孱沐Ъ弘妁ы麸脲瞽篝狎舂┅ㄤ彐躅轭箦螋眙镫孱箦铘孱沐眙镫孱脲ㄩ姝屮轶趔т锃铒翳轭绌ㄩ铙弪舡眙镫孱箦铘孱沐扉篝眙镫孱洪姝屮轶趔殒屮轶趔┅ㄤ彐躅蝈盹鲥眙镫孱箦铘孱沐篝狎舂戾è眙镫孱麸蝈盹鲥ㄦ轭洵殒灬礅溽眙氅ㄥ聃犰眙镫孱篝狎眙氅篝狎舂箦铘孱沐眙镫孱箦铘孱沐┅┅ㄣ镱眙镫孱麸蝈盹鲥箦翩箦铘孱沐眙镫孱箦铘孱沐蝈盹鲥眙镫孱麸蝈盹鲥箦铘孱沐眙镫孱箦铘孱沐┅箦铘孱沐ㄦ矧磲⒆烈紊吻澡弪濮铒眭祠轺矧麸脲篝狎糸铉鏖翳麸蝈盹鲥ア篝狎舂箦铘孱沐┅┅ㄤ彐躅眙镫孱爵镫孱箦铘孱沐眙镫孱蝈盹鲥殒铒灬礅溽ㄡ钿窘眙镫孱篝狎眙镫孱┅冀眙镫孱孱眙镫孱┅┅箦铘孱沐麸脲铙箦铘孱沐弘妁麸脲瞽殇┅ㄤ彐躅瘐箬麸脲箦铘孱沐轭箦螋邃麸脲瞟深箦螋麸脲狒箦铘孱沐镡赍泗徐遽箦铒轸黠瞌忮轭箦螋邃屮徙綮狍玳鲥詈轸赡鏖祆忮翳筢礤痨徙麒弪轸ъ忮轭箦螋邃怩轸桢徜箬秕熹痫轭麸殇鲠祯痱轱麸翳轭箦螋轱町ㄤ镬轶麸脲箦铘孱沐麸脲铙箦铘孱沐┅麒孱窘麸脲瞽殇麸脲瞟麸脲瞽殇轭箦螋邃麸脲瞟箦翩箪雉鲠祯麸脲ч洎ū箪雉鲠祯麸脲ч洎┅麒孱窘麸脲瞽桢徜麸脲瞟麸脲瞽殇轭箦螋邃麸脲瞟箦翩箪雉鲠祯麸脲ц遽洎ū箪雉鲠祯麸脲ц遽洎┅┅灬忮祗è殒屮轶趔箪雉钺礤ㄩ箪雉怙躅漯轭箦螋邃麸脲箪雉钺礤箪雉鲠祯轭箦螋邃麸脲箪雉钺礤⑦┅ㄩ铙弪舡狒箦铘孱沐麸脲铙箦铘孱沐ū麸脲瞽殇轭箦螋邃麸脲瞟磲脲轭篝犷沐麸脲洪麸脲瞽殇轭箦螋邃麸脲瞟烘矧ㄩ姝屮轶趔ф矧愆红屙磲ㄩ姝屮轶趔ъ屙磲乎痫篝徵ㄩ姝屮轶趔躔矬翎绌壶痫篝徵ㄩ姝屮轶趔痫篝徵烘遽趔ㄩ姝屮轶趔ф遽趔鸿遽ㄩ窘麸脲瞽桢徜轭箦螋邃麸脲瞟麸脲瞽殇轭箦螋邃麸脲瞟ū麸脲瞽桢徜轭箦螋邃麸脲瞟麸脲瞽桢徜轭箦螋邃麸脲瞟轰屦蝈ㄩ姝屮轶趔т屦蝈飑轰屦ㄩ姝屮轶趔т屦螬喉轶ㄩ姝屮轶趔ы轶悌┅┅ㄤ彐躅痫瓠麸脲箦铘孱沐殇戾è蝈盹鲥洵麸脲ㄦ轭殇箦铘孱沐麸脲铙箦铘孱沐弘妁＇麸脲瞽殇┅ㄩ蟓桢徜ㄦ轭洵殒灬礅溽ㄥ聃犰殇麸脲瞽桢徜┅箦铘孱沐麸脲铙箦铘孱沐┅ㄩ瞽眙镫孱ㄤ镬轶ㄩ铘弪鲠磲疸狎＇灬礅溽眙氅扉篝眙镫孱篝狎眙氅眙镫孱孱眙氅┅箦铘孱沐眙镫孱箦铘孱沐┅ㄣ镱èㄣ狎轭翦蝣犰殇蝈趱蝾铋飑è窘ㄣ徜轭翦蝣犰殇蝈趱蝾轭翦蝣犰┅┅┅ㄣ镱è铛祆蝈盹鲥洵麸脲瞟ㄦ矧磲⒆烈紊吻澡弪濮铒麸脲岙ア殇箦铘孱沐ㄩ瞽眙镫孱ㄦ矧磲⒆烈紊吻澡轶麸脲轶泔铘衢铄轭翳眭祠轺矧麸脲岘狺ノ雉蝈盹鲩铉ヒ屙秭翳眭祠轺矧麸脲忮骘蝈蝈盹鲩铉翳轶麸脲町ア轭眙镫孱箦铘孱沐ㄩ蟓桢徜ㄦ矧磲⒆烈紊吻澡轶麸脲轶翳桢徜镦麸脲岙ノ雉蝈盹鲩铉バ戾狍汨犷珏轸桢徜忮骘蝈蝈盹鲩铉翳轶麸脲町ア麸脲瞽殇轶桢徜┅箦铘孱沐箦翩箪雉鲠祯箦铘孱沐麸脲铙蝈盹鲥蝈盹鲥洵麸脲箦铘孱沐麸脲铙箦铘孱沐┅ㄤ镬轶麸脲箦铘孱沐麸脲铙箦铘孱沐┅麒孱麸脲瞽殇麸脲瞟殇箦翩箪雉鲠祯麸脲ч洎ū箪雉鲠祯麸脲ч洎┅ㄣ镱è麸脲瞽桢徜麸脲瞟殇箦翩箪雉鲠祯麸脲ц遽洎ū箪雉鲠祯麸脲ц遽洎┅è麸脲瞽桢徜麸脲瞟殇箦翩箪雉鲠祯麸脲ц遽洎⑦┅┅蝈盹鲥洵麸脲瞟┅ㄤ彐躅箦舡桢徜箦铘孱沐殇铄鳝桢徜镳糸镱犰溴痱屐戾è麸脲ㄦ轭殇箦铘孱沐麸脲铙箦铘孱沐弘妁＇麸脲瞽殇┅ㄤ弩沐钿犷艨ㄩ蟓溴筱孱溽铘铄鳝桢徜殇箦铘孱沐┅ㄣ镱ㄤ弩沐钿犷艨ㄦ矧磲⒘麸脲汜铑雉栳鲥溴筱孱溽铘狍桢徜ピ镫孱轶溴筱孱溽铘镦麸脲岈鲩疳翳岈. Nothing changed.%% "
	       new-head id descendant?))
      ((equal id new-head)
       (format t "A token cannot have itself as head. Nothing changed.%%"))
      (t
       (setf (slot-value token 'head)
	     new-head)
       (if deprel
	   (setf (slot-value token 'deprel)
		 deprel))))))


(defun is-descendant? (id-1 id-2 sentence)
  (is-descendant?-aux id-1 id-2 sentence nil))


(defun is-descendant?-aux (id-1 id-2 sentence alist)
  (let ((father-of-1-id (token-head
			 (find id-1 (sentence-tokens sentence) :key #'token-id))))
    (cond
      ((eq father-of-1-id 0)
       nil)
      ((eq father-of-1-id id-2)
       (reverse (cons id-2 (cons id-1 alist))))
      (t
       (is-descendant?-aux father-of-1-id id-2 sentence (cons id-1 alist))))))

