
(in-package :cl-conllu)

(defclass token ()
  ((id      :initarg :id
	    :accessor token-id)
   (form    :initarg :form
	    :accessor token-form)
   (lemma   :initarg :lemma
	    :accessor token-lemma)
   (upostag :initarg :upostag
	    :initform "_"
	    :accessor token-upostag)
   (xpostag :initarg :xpostag
	    :initform "_"
	    :accessor token-xpostag)
   (feats   :initarg :feats
	    :initform "_"
	    :accessor token-feats)
   (head    :initarg :head
	    :initform "_"
	    :accessor token-head)
   (deprel  :initarg :deprel
	    :initform "_"
	    :accessor token-deprel)
   (deps    :initarg :deps
	    :initform "_"
	    :accessor token-deps)
   (misc    :initarg :misc
	    :initform "_"
	    :accessor token-misc)))

(defclass mtoken ()
  ((start   :initarg :start
	    :accessor mtoken-start)
   (end     :initarg :end
	    :accessor mtoken-end)
   (form    :initarg :form
	    :accessor mtoken-form)
   (misc    :initarg :misc
	    :initform "_"
	    :accessor mtoken-misc)))

(defclass sentence ()
  ((start   :initarg :start
	    :initform 0
	    :accessor sentence-start)
   (meta    :initarg :meta
	    :initform nil
	    :accessor sentence-meta)
   (tokens  :initarg :tokens
	    :initform nil
	    :accessor sentence-tokens)
   (mtokens :initarg :mtokens
	    :initform nil
	    :accessor sentence-mtokens)))


(defun sentence-meta-value (sentence meta-field)
  (cdr (assoc meta-field (sentence-meta sentence) :test #'equal)))


(defun sentence->text (sentence)
  (labels ((forma (obj lst)
	     (if (search "SpaceAfter=No" (slot-value obj 'misc))
		 (cons (slot-value obj 'form) lst)
		 (cons " " (cons (slot-value obj 'form) lst))))
	   (aux (tokens mtokens ignore response)
	     (cond 
	       ((and (null tokens) (null mtokens))
		(if (equal " " (car response))
		    (reverse (cdr response))
		    (reverse response)))

	       ((and ignore (< (token-id (car tokens)) ignore))
		(aux (cdr tokens) mtokens ignore response))
	       ((and ignore (equal (token-id (car tokens)) ignore))
		(aux (cdr tokens) mtokens nil response))
      
	       ((and mtokens (<= (mtoken-start (car mtokens)) (token-id (car tokens))))
		(aux tokens (cdr mtokens)
				   (mtoken-end (car mtokens))
				   (forma (car mtokens) response)))
	       (t
		(aux (cdr tokens) mtokens ignore (forma (car tokens) response))))))
    (format nil "狺ㄡ貘箦铘孱沐麸脲铙箦铘孱沐箦铘孱沐眙镫孱箦铘孱沐铋铋飑┅ㄤ彐躅箦铘孱沐鲠扉淇箦铘孱沐ㄡ钿ㄥ鲥蝙灬礅溽綦铒ㄥ聃犰箪雉鲠祯綦ч洎箪雉鲠祯綦ц遽洎┅箦铘孱沐麸脲铙箦铘孱沐┅箫礤灬礅溽綦ㄡ钿ㄥ聃犰箪雉鲠祯綦ц遽洎ㄥ聃犰Ⅱ镲簪箪雉鲠祯綦т屦蝈飑┅箦铘孱沐麸脲铙箦铘孱沐┅箦铘孱沐礤翎鲠祯箦铘孱沐Ⅳ屮簪ㄥ聃犰箦铘孱沐礤翎鲠祯箦铘孱沐Ⅳ屮簪箦铘孱沐爵屮箦铘孱沐┅箦铘孱沐礤翎鲠祯箦铘孱沐Ⅲ孱暨殇┅ㄤ彐躅箦铘孱沐箝箦铘孱沐戾铉翳箦铘孱沐麸脲铙箦铘孱沐┅ㄤ彐躅箦铘孱沐句邋箦铘孱沐脲骖脲灬忮祗è孱篚蝈扉篝脲ㄩ簌礅镬脲扉篝骖脲脲┅ㄩㄦ躅泗轱铕骖脲ㄤ邋瓠狨箦铘孱沐蝻雉箦铘孱沐箦铘孱沐骖脲ㄩ矧簌礅镬骖脲扉篝骖脲┅ㄤ邋瓠狨箦铘孱沐蝻雉箦铘孱沐箦铘孱沐灬礅溽綦戾è秕祜镳骘轭ㄥ铙躜瀛扉篝骖脲泔祆邈箪雉鲠祯綦氅┅ㄩㄡ钿扉篝秕舂戾铉翳秕舂┅ㄣ狎秕舂秕舂┅┅┅ㄤ彐躅溴屦狨蝻雉箦铘孱沐骖脲扉篝ㄦ躅汜祆骖脲蝻雉祜镳骘汨殪轭麸脲瞽汨殪蝻雉箦铘孱沐泔祆邈扉篝箪雉鲠祯汨殪т屦蝈飑ㄩ麸脲瞽汨殪汨殪箦铘孱沐ㄤ邋瓠狨汨殪箦铘孱沐骖脲ㄦ躅汜祆骖脲汨殪洎┅┅ㄤ彐躅箦铘孱沐蝻雉箦铘孱沐ㄣ狎蝈盹鲥殒铒灬礅溽綦ㄥ聃犰阿箪雉鲠祯綦ц遽洎┅箦铘孱沐麸脲铙箦铘孱沐┅┅ㄤ彐躅麸脲瞽汨殪麸脲箦铘孱沐蝈盹鲥殒铒灬礅溽綦ㄥ聃犰箪雉鲠祯綦ц遽洎箪雉鲠祯麸脲ч洎┅箦铘孱沐麸脲铙箦铘孱沐┅ㄤ彐躅眙镫孱爵镫孱箦铘孱沐眙镫孱蝈盹鲥殒铒灬礅溽ㄡ钿窘眙镫孱篝狎眙镫孱┅冀眙镫孱孱眙镫孱┅┅箦铘孱沐麸脲铙箦铘孱沐弘妁麸脲瞽殇┅ㄤ彐躅轭箦螋麸脲箦铘孱沐铄鳝麸脲瞟⑸铙弪趔麸脲轭箦铘孱沐镡赍泗婶鏖祆铒忮轭箦螋邃屮徙綮狍玳鲥詈轸赡鏖祆忮翳筢礤痨徙麒弪轸ъ忮轭箦螋邃怩轸桢徜箬秕熹痫轭麸殇鲠祯痱轱麸翳轭箦螋轱町婶汨犷珏翳箦铘孱沐镡赍泗疳篌邃鏖翳箪雉麸脲铙箦铘孱沐ㄤ镬轶麸脲麸脲铙ㄩ窘麸脲瞽殇麸脲瞟麸脲瞽殇铄鳝麸脲瞟ㄩ钽麸脲瞽殇麸脲瞟┅ㄩ窘麸脲瞽桢徜麸脲瞟麸脲瞽殇铄鳝麸脲瞟ㄩ钽麸脲瞽桢徜麸脲瞟┅ㄩ窘麸脲瞽桢徜铄鳝麸脲瞟麸脲瞽殇铄鳝麸脲瞟ㄩ钽麸脲瞽桢徜铄鳝麸脲瞟┅ㄩ铙弪舡狒麸脲铙ū麸脲瞽殇铄鳝麸脲瞟铄鳝麸脲瞟箦铘孱沐┅ㄤ彐躅蝈盹鲥麸脲箦铘孱沐殇⒁屙秭翳麸脲鏖翳翳玳鲥赡殒轸轶铒疳螋镦眭祠榄黠蜾麸脲犷轸滹弩铒泔铘衢汨殪潴婶蝈趱蝾赭鲠祯弩翳箦铘孱沐ㄣ栳铉邃矧铒舂犷怙镬遽铋殒翳箦铘孱沐麽铒汨犷珏犷趄蹂殒汨犷珏洚涉翳蝈盹鲥麸脲轶翳蝻雉镦翳箦铘孱沐铄蝻雉眭篝忮痱秭殇邃鏖翳箪雉麸脲铙眙镫孱螬箦铘孱沐戾è麸蝈盹鲥ㄦ轭殇麸脲铙弘妁＇麸脲瞽殇呼弩＇羼踽飑ㄣ栝熹ㄦ轭殇麸脲铙弘妁＇麸脲瞽桢徜呼弩＇羼踽飑┅ㄣ镱è箫礤灬礅溽眙冀眙镫孱篝狎眙殇眙镫孱孱眙┅眙镫孱螬鲠祯弩箦铘孱沐铋飑è矧铛祆麸蝈盹鲥汨殪潴鲠祯弩箦铘孱沐铋飑ㄤ镬轶麸脲箦铘孱沐麸脲铙箦铘孱沐┅ㄩ麸脲瞽殇麸脲瞟殇ㄤ邈麸脲瞽殇麸脲瞟┅ㄩ麸脲瞽桢徜麸脲瞟殇ㄤ邈麸脲瞽桢徜麸脲瞟┅箦翩麸脲铙蝈盹鲥麸蝈盹鲥麸脲铙┅鲠祯弩箦铘孱沐舂┅┅ㄤ彐躅箦舡桢徜箦铘孱沐殇铄鳝桢徜镳糸镱犰溴痱屐戾è麸脲ㄦ轭殇箦铘孱沐麸脲铙箦铘孱沐弘妁＇麸脲瞽殇呼弩＇羼踽飑┅ㄣ镱è轶溴筱孱溽铘铄鳝桢徜殇箦铘孱沐鲠祯弩箦铘孱沐铋飑è羼踽殇铄鳝桢徜鲠祯弩箦铘孱沐铋飑箦翩箪雉鲠祯麸脲ц遽洎铄鳝桢徜ㄩ溴痱屐箦翩麸脲瞽溴痱屐т屦蝈飑溴痱屐┅鲠祯弩箦铘孱沐舂┅┅ㄤ彐躅轶溴筱孱溽铘ㄩ洵殇箦铘孱沐镳糸镱犰犰轶舂戾è疳蝈铘麸脲瞽桢徜ㄦ轭殇箦铘孱沐麸脲铙箦铘孱沐弘妁＇麸脲瞽殇呼弩＇羼踽飑┅ㄣ镱è羼踽疳蝈铘癌铋飑è羼踽疳蝈铘殇博蝈鲥蝮ㄣ镱殇ㄣ镱殇犰轶舂┅ㄩ蟓溴筱孱溽铘疳蝈铘殇箦铘孱沐ㄣ镱殇犰轶舂┅┅ㄤ彐躅徜牾篝箦铘孱沐箦铘孱沐⒁邈彘鲥箦铘孱沐犷蝈孱蹴弪狒赡犷扰聊鲠祯弩镦遽汨麸脲箫翳狒翳彘矧溴ㄡ轭箦铘孱沐麸脲铙轶蝈箴邈翦洚鏖翳箪雉麸脲铙眙镫孱螬箦铘孱沐戾è磲痼ㄣ镱啜癌磲疸狎灬礅溽綦痫螬ㄣ镱麸脲瞽殇綦ū痫螬┅麸脲铙蜥铉戾铉翳麸脲铙┅┅┅ㄤ镬轶綦麸脲铙箦翩麸脲瞽殇綦ㄣ潋ㄡ篌镢麸脲瞽殇綦磲痼┅麸脲瞽桢徜綦ㄣ潋ㄡ篌镢麸脲瞽桢徜綦磲痼┅┅ㄤ镬轶眙眙镫孱螬箦翩眙镫孱篝狎眙氅ㄣ潋ㄡ篌镢眙镫孱篝狎眙氅磲痼┅眙镫孱孱眙氅ㄣ潋ㄡ篌镢眙镫孱孱眙氅磲痼┅┅┅箦铘孱沐